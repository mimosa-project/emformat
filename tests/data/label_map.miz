begin :: Semilattice of type widening

definition
  let T be RelStr;
  attr T is Noetherian means
  :Def1:
  the InternalRel of T is co-well_founded;
end;

definition
  let T be non empty RelStr;
  redefine attr T is Noetherian means
  :Def2:
  for A being non empty Subset of T
  ex a being Element of T st a in A & for b being Element of T st b in A holds
  not a < b;
  compatibility
  proof
    set R = the InternalRel of T;
    thus T is Noetherian implies for A being non empty Subset of T ex a being
    Element of T st a in A & for b being Element of T st b in A holds not a < b
    proof
      assume
A10:   for Y being set st Y c= field R & Y <> {}
      ex a being object st a in
      Y & for b being object st b in Y & a <> b holds not [a,b] in R;
      let A be non empty Subset of T;
      set a = the Element of A;
      reconsider a as Element of T;
      set Y = A /\ field R;
      per cases;
      suppose
A20:     A misses field R;
        take a;
        thus a in A;
        let b be Element of T;
        assume that
        b in A and
A30:     a < b;
        a <= b by A30,ORDERS_2:def 6;
        then [a,b] in R by ORDERS_2:def 5;
        then a in field R by RELAT_1:15;
        hence contradiction by A20,XBOOLE_0:3;
      end;
      suppose
        A meets field R;
        then Y <> {};
        then consider x being object such that
A4:     x in Y and
A5:     for y being object st y in Y & x <> y holds not [x,y] in R by A10,
XBOOLE_1:17;
        reconsider x as Element of T by A4;
        take x;
        thus x in A by A4,XBOOLE_0:def 4;
        let b be Element of T;
        assume that
A6:     b in A and
A7:     x < b;
        x <= b by A7,ORDERS_2:def 6;
        then
A8:     [x,b] in R by ORDERS_2:def 5;
        then b in field R by RELAT_1:15;
        then b in Y by A6,XBOOLE_0:def 4;
        hence contradiction by A5,A7,A8;
      end;
    end;
    assume
A9: for A being non empty Subset of T ex a being Element of T st a in
    A & for b being Element of T st b in A holds not a < b;
    let Y be set;
    assume that
A100: Y c= field R and
A110: Y <> {};
    field R c= (the carrier of T) \/ the carrier of T by RELSET_1:8;
    then reconsider A = Y as non empty Subset of T by A100,A110,XBOOLE_1:1;
    consider a being Element of T such that
A120: a in A and
A130: for b being Element of T st b in A holds not a < b by A9;
    take a;
    thus a in Y by A120;
    let b be object;
    assume that
A140: b in Y and
A150: a <> b;
    b in A by A140;
    then reconsider b as Element of T;
    not a < b by A130,A140;
    then not a <= b by A150,ORDERS_2:def 6;
    hence thesis by ORDERS_2:def 5;
  end;
end;

theorem Th1:
  for T being Noetherian sup-Semilattice for I being Ideal of T
  holds ex_sup_of I, T & sup I in I
proof
  let T be Noetherian sup-Semilattice;
  let I be Ideal of T;
  consider a being Element of T such that
A10: a in I and
A20: for b being Element of T st b in I holds not a < b by Def2;
A30: I is_<=_than a
  proof
    let d be Element of T;
    assume d in I;
    then a"\/"d in I by A10,WAYBEL_0:40;
    then
A4: not a < a"\/"d by A20;
    a <= a"\/"d by YELLOW_0:22;
    then a = a"\/"d by A4,ORDERS_2:def 6;
    hence thesis by YELLOW_0:22;
  end;
  for c being Element of T st I is_<=_than c holds a <= c by A10;
  hence thesis by A10,A30,YELLOW_0:30;
end;