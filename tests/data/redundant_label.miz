:: Zero Based Finite Sequences
::  by Tetsuya Tsunetou , Grzegorz Bancerek and Yatsuka Nakamura
::
:: Received September 28, 2001
:: Copyright (c) 2001-2021 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, SUBSET_1, FUNCT_1, ARYTM_3, XXREAL_0, XBOOLE_0, TARSKI,
      NAT_1, ORDINAL1, FINSEQ_1, CARD_1, FINSET_1, RELAT_1, PARTFUN1, FUNCOP_1,
      ORDINAL4, ORDINAL2, ARYTM_1, REAL_1, ZFMISC_1, FUNCT_4, VALUED_0,
      AFINSQ_1, PRGCOR_2, CAT_1, AMISTD_1, AMISTD_3, AMISTD_2, VALUED_1,
      CONNSP_3, XCMPLX_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1,
      CARD_1, ORDINAL2, NUMBERS, ORDINAL4, XCMPLX_0, XREAL_0, NAT_1, PARTFUN1,
      BINOP_1, FINSOP_1, NAT_D, FINSET_1, FINSEQ_1, FUNCOP_1, FUNCT_4, FUNCT_7,
      XXREAL_0, VALUED_0, VALUED_1;
 constructors WELLORD2, FUNCT_4, XXREAL_0, ORDINAL4, FUNCT_7, ORDINAL3,
      VALUED_1, ENUMSET1, NAT_D, XXREAL_2, BINOP_1, FINSOP_1, RELSET_1, CARD_1,
      NUMBERS;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCOP_1,
      XXREAL_0, XREAL_0, NAT_1, CARD_1, ORDINAL2, NUMBERS, VALUED_1, XXREAL_2,
      MEMBERED, FINSET_1, FUNCT_4, FINSEQ_1, INT_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, ORDINAL1, XBOOLE_0, RELAT_1, PARTFUN1, CARD_1;
 equalities ORDINAL1, FUNCOP_1, VALUED_1;
 expansions TARSKI, ORDINAL1, RELAT_1, CARD_1, FUNCT_1;
 theorems TARSKI, AXIOMS, FUNCT_1, NAT_1, ZFMISC_1, RELAT_1, RELSET_1,
      ORDINAL1, CARD_1, FINSEQ_1, FUNCT_7, ORDINAL4, CARD_2, FUNCT_4, ORDINAL3,
      XBOOLE_0, XBOOLE_1, FINSET_1, FUNCOP_1, XREAL_1, VALUED_0, ENUMSET1,
      XXREAL_0, XREAL_0, GRFUNC_1, XXREAL_2, NAT_D, VALUED_1, XTUPLE_0,
      FINSEQ_3, ORDINAL2, INT_1;
 schemes FUNCT_1, SUBSET_1, NAT_1, XBOOLE_0, CLASSES1, FINSEQ_1;

begin

reserve k,n for Nat,
  x,y,z,y1,y2 for object,X,Y for set,
  f,g for Function;

:: Extended Segments of Natural Numbers

theorem Th0: ::: CHORD:1 moved eventually from there -> go to INT_1
  for n being non zero Nat holds n-1 is Nat & 1 <= n
  proof
   let n be non zero Nat;
A1: 0+1 <= n by NAT_1:13;
   0+1-1 <= n-1 by A1, XREAL_1:9;
   then n-1 in NAT by INT_1:3;
   hence n-1 is Nat;
   thus thesis by A1;
end;

theorem Th1:
  Segm n \/ { n } = Segm(n+1)
proof
   n in Segm(n+1) by NAT_1:45;
   then
A1:{n} c= Segm(n+1) by ZFMISC_1:31;
   Segm n c= Segm(n+1) by NAT_1:39,11;
  hence Segm n \/ { n } c= Segm(n+1) by A1,XBOOLE_1:8;
  let x be object;
  assume
A2: x in Segm(n+1);
    then reconsider x as Nat;
  now
B1: x < n+1 by A2,NAT_1:44;
  per cases by B1,NAT_1:22;
   case x < n;
    hence x in Segm n by NAT_1:44;
   end;
   case x = n;
    hence x in {n} by TARSKI:def 1;
   end;
  end;
 hence thesis by XBOOLE_0:def 3;
end;

theorem Th2:
  Seg n c= Segm(n+1)
proof
  let x be object;
  assume
A1: x in Seg n;
  then reconsider x as Element of NAT;
  x<=n by A1,FINSEQ_1:1;
  then x<n+1 by NAT_1:13;
  hence thesis by NAT_1:44;
end;

theorem
  n+1 = {0} \/ Seg n
proof
  thus n+1 c= {0} \/ Seg n
  proof
   let x be object;
    assume x in n+1;
    then x in {j where j is Nat: j<n+1} by AXIOMS:4;
    then consider j being Nat such that
A1: j=x and
A2: j<n+1;
C1: j=0 or 1<j+1 & j<=n by A2,NAT_1:13,XREAL_1:29;
    j=0 or 1<=j & j<=n by C1,NAT_1:13;
    then x in {0} or x in Seg n by A1,FINSEQ_1:1,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 3;
  end;
A3: Segm 1 c= Segm(n+1) by NAT_1:39,11;
  Seg(n) c= Segm(n+1) by Th2;
  hence thesis by A3,CARD_1:49,XBOOLE_1:8;
end;